apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group='com.github.ivianuu'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['autovaluegson.defaultCollectionsToEmpty': 'true']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Android Support
    api 'com.android.support:support-annotations:26.0.2'

    // AutoValue
    provided 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.google.auto.value:auto-value:1.4.1'

    // AutoValue Gson
    provided 'com.ryanharter.auto.value:auto-value-gson:0.5.0'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.5.0'

    // AutoValue Parcel
    provided 'com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.5'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    // Gson
    api 'com.google.code.gson:gson:2.8.1'

    // Retrofit
    api 'com.squareup.retrofit2:retrofit:2.2.0'
    api 'com.squareup.retrofit2:converter-gson:2.2.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    // Ok http
    api 'com.squareup.okhttp3:okhttp:3.8.0'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
